apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'cool.graph'
archivesBaseName = 'cuid-java'
version '0.1.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'cuid-java'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Collision-resistant ids for Java.'
                url 'https://github.com/graphcool/cuid-java'

                scm {
                    connection 'scm:git:git@github.com:graphcool/cuid-java.git'
                    developerConnection 'scm:git:git@github.com:graphcool/cuid-java.git'
                    url 'git@github.com:graphcool/cuid-java.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'marktani'
                        name 'Nilan Marktanner'
                        email 'nilan.marktanner@gmail.com'
                    }
                }
            }
        }
    }
}
